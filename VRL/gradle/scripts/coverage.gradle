// **** Coverage Report Config ****
// 
//

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

repositories {
    mavenCentral()
}

allprojects {
    apply plugin: 'jacoco'
}

// multiproject coverage report
// http://csiebler.github.io/blog/2014/02/09/multi-project-code-coverage-using-gradle-and-jacoco/
task codeCoverageReport(type: JacocoReport) {

    // Gather execution data from all subprojects
    // (change this if you e.g. want to calculate unit test/integration test coverage separately)
    executionData fileTree(project.rootDir.absolutePath).include("**/${buildDir}/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects 
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        // xml reports are used for coveralls
        xml.enabled true
        xml.destination = "${buildDir}/reports/jacoco/xml/report.xml"
        
        // html for ulpading to webserver
        html.enabled true
        html.destination "${buildDir}/reports/jacoco/html"
        
        // we don't need csv
        csv.enabled false
    }
}

coveralls {
    
    // specify jacoco xml report location
    jacocoReportPath = "${buildDir}/reports/jacoco/xml/report.xml"
    
    // add sources from the subprojects
    sourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs).files.absolutePath
}

// always run the tests before generating the report
codeCoverageReport.dependsOn {
    subprojects*.test
}

